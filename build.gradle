apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "idea"

// Uncomment if your project has an API JAR
//apply from: "api.gradle"

// define the version for the project when publishing to maven
group "edu.monument"
version "1.0-SNAPSHOT"

ext {
  learnVersion = "3900.6.0"
  deployServer = "http://localhost:8080"
  deployInstance = "BBLEARN"
}

repositories {
  maven {
    url "https://maven.blackboard.com/content/repositories/releases/"
  }
  mavenCentral()
}

configurations {
  buildUtils
}

// define the project's dependencies
dependencies {
  providedCompile "javax.servlet:javax.servlet-api:3.1.0",
                  "javax.servlet.jsp:javax.servlet.jsp-api:2.3.3"

  // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
  // You should NEVER include Learn JARs (other than webapis) in your B2.
  providedCompile( "blackboard.platform:bb-platform:$project.ext.learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-taglibs:$project.ext.learnVersion" ) { transitive = false }

  // Building a Spring B2?  Use our Spring APIs!
  compile "blackboard.platform:bb-spring-webapi:10.1"

  // Include the Blackboard security APIs for protecting your B2.
  // providedCompile "blackboard.plugin:security:1.0:api"

  // Dependencies that are only necessary at runtime so should be included in the WAR
  // runtime "org.javassist:javassist:3.17.1-GA"

  buildUtils "org.oscelot:b2deploy-task:0.1.0"
}

// Add a task to deploy a B2 using starting block
task deployB2() {
  dependsOn 'war'
  doLast {
    ant.taskdef( name: "b2deploy",
                 classname: "org.oscelot.ant.B2DeployTask",
                 classpath: project.configurations.buildUtils.asPath
    )
    ant.b2deploy( localfilepath: project.war.archivePath,
                  host: project.ext.deployServer,
                  courseorgavailable: 'true',
                  clean: 'true',
                  webappName: 'bb-starting-block-' + project.ext.deployInstance
    )
  }
}
